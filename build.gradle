apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'groovy'
apply from: file("etc/emma.gradle")

group = 'io.qdb'
version = '0.1.0-SNAPSHOT'
mainClassName = 'io.qdb.server.Main'

repositories {
    mavenCentral()
    maven {
        url "/Users/david/.m2/repository"
    }
}

dependencies {
    compile 'io.qdb:qdb-buffer:0.3.0-SNAPSHOT'
    compile 'org.simpleframework:simple:4.1.21'
    compile 'com.google.inject:guice:3.0'
    compile 'ch.qos.logback:logback-core:1.0.7'
    compile 'ch.qos.logback:logback-classic:1.0.7'
    compile 'org.slf4j:slf4j-api:1.6.6'
    compile 'com.typesafe:config:1.0.0'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.7'
    compile 'com.google.guava:guava:13.0.1'
    compile('com.netflix.curator:curator-recipes:1.2.5') {
        exclude group: 'org.slf4j'
        exclude group: 'com.sun.jmx', module: 'jmxri'
        exclude group: 'com.sun.jdmk', module: 'jmxtools'
        exclude group: 'javax.jms', module: 'jms'
        exclude group: 'log4j'
        exclude group: 'jline'
    }

    groovy "org.codehaus.groovy:groovy-all:1.8.6"
    testCompile "org.spockframework:spock-core:0.6-groovy-1.8"
    testCompile 'junit:junit:4.10'
    testCompile 'commons-io:commons-io:2.4'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar
}

task("run-debug", dependsOn: 'classes', type: JavaExec) {
    main = "io.qdb.server.Main"
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005"]
}
